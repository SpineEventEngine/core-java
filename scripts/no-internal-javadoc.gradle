//
// Copyright 2017, TeamDev Ltd. All rights reserved.
//
// Redistribution and use in source and/or binary forms, with or without
// modification, must retain the above copyright notice and the following
// disclaimer.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
//

repositories {
    // Spine releases repository.
    maven { url = 'http://maven.teamdev.com/repository/spine' }

    // Spine snapshots repository.
    maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
}

configurations {
    excludeInternalDoclet
}

dependencies {
    excludeInternalDoclet group: 'io.spine.tools', name: 'spine-javadoc', version: spineToolsVersion
    //TODO:2017-03-01:dmytro.grankin: Remove dependency after update guava to 22.0.
    // We need this in order to generate Javadoc correctly.
    // Details: https://github.com/google/guava/issues/2721
    compile group: 'com.google.errorprone', name: 'error_prone_annotations', version: '2.0.15'
}

task noInternalJavadoc(type: Javadoc) {
    source = sourceSets.main.allJava.filter {
        !it.absolutePath.contains('generated')
    }
    classpath = javadoc.getClasspath()

    options {
        // Doclet fully qualified name.
        doclet = 'io.spine.tools.javadoc.ExcludeInternalDoclet'

        // Path to the JAR containing the doclet.
        docletpath = configurations.excludeInternalDoclet.files.asType(List)

        // Encoding of the source files.
        encoding = 'UTF8'
    }
}
