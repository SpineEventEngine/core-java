/*
 * Copyright 2018, TeamDev Ltd. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

import groovy.time.TimeCategory

buildscript {

    // Applying from `ext.gradle` inside the `buildscript` section to reuse the properties.
    //
    // As long as `buildscript` section is always evaluated first, we need to apply
    // `ext.gradle` explicitly here.
    apply from: 'ext.gradle'

    ext {
        googleMavenCentralMirror = 'https://maven-central.storage.googleapis.com'
        spineRepository = 'http://maven.teamdev.com/repository/spine'
        spineSnapshotsRepository = 'http://maven.teamdev.com/repository/spine-snapshots'
    }

    repositories {
        jcenter()
        maven { url = googleMavenCentralMirror }

        mavenCentral()
        mavenLocal()

        // Spine releases repository.
        maven { url = spineRepository }

        // Spine snapshots repository.
        maven { url = spineSnapshotsRepository }
    }

    dependencies {
        classpath group: 'com.google.guava', name: 'guava', version: guavaVersion
        classpath(group: 'com.google.protobuf', name: 'protobuf-gradle-plugin',
                                                version: protobufGradlePluginVerison) {
            // exclude an old Guava version
            exclude group: 'com.google.guava'
        }
        classpath group: 'io.spine.tools', name: 'spine-model-compiler',
                                           version: spineBaseVersion
        /* Uncomment the below statement if you want the dependency to be fetched on each build.
           Please note that offline builds will not be available then. */
                                             //, changing: true

        classpath group: 'io.spine.tools', name: 'spine-codestyle-checker',
                                           version: spineToolsVersion
        classpath group: 'io.spine.tools', name: 'gcs-plugin', version: spineToolsVersion
    }
}

apply from: 'ext.gradle'

ext {
    protobufDependency = "com.google.protobuf:protoc:${protobufVersion}"
    spineCodestyleCheckerId = 'io.spine.tools.spine-codestyle-checker'
    spineGcsPlugin = 'io.spine.tools.gcs-plugin'
    spineJacocoScript = "${rootDir}/scripts/jacoco.gradle"
    spineProtobufPluginId = 'io.spine.tools.spine-model-compiler'

    credentialsPropertyFile = 'credentials.properties'
    publishPlugin = "$rootDir/scripts/publish.gradle"
    projectsToPublish = ["core",
                         "client",
                         "server",
                         "testutil-core",
                         "testutil-client",
                         "testutil-server",
                         "model-assembler",
                         "model-verifier"]
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'idea'
    apply plugin: 'project-report'

    group = 'io.spine'
    version = spineVersion
}

final boolean isTravis = System.env.TRAVIS == 'true'

/**
 * Determines whether the given {@code project} should expose its Javadoc to
 * SpineEventEngine.github.io website.
 *
 * <p>Currently, the {@code testutil} projects are excluded from publishing, as well as the modules
 * that perform the model compile-time checks.
 *
 * @param project the project to check
 * @return {@code true} is the project Javadoc should be published, {@code false} otherwise
 */
static boolean shouldPublishJavadoc(final Project project) {
    return !project.name.startsWith('testutil') && !project.name.startsWith("model")
}

subprojects {

    configurations {
        // Avoid collisions of Java classes defined both in `protobuf-lite` and `protobuf-java`
        runtime.exclude group: "com.google.protobuf", module: "protobuf-lite"
        testRuntime.exclude group: "com.google.protobuf", module: "protobuf-lite"
    }

    project.ext {
        sourcesRootDir = "$projectDir/src"
        generatedRootDir = "$projectDir/generated"

        generatedJavaDir = "$generatedRootDir/main/java"
        generatedTestJavaDir = "$generatedRootDir/test/java"

        generatedGrpcDir = "$generatedRootDir/main/grpc"
        generatedTestGrpcDir = "$generatedRootDir/test/grpc"

        generatedSpineDir = "$generatedRootDir/main/spine"
        generatedTestSpineDir = "$generatedRootDir/test/spine"

        testArtifactsScript = "${rootDir}/scripts/test-artifacts.gradle"
        generateDescriptorSetScript = "${rootDir}/scripts/generate-descriptor-set.gradle"
        filterInternalJavadocsScript = "${rootDir}/scripts/no-internal-javadoc.gradle"
        gRpcScript = "${rootDir}/scripts/grpc.gradle"
        updateDocsPlugin = "$rootDir/scripts/update-gh-pages.gradle"
    }

    apply plugin: 'com.google.protobuf'
    apply plugin: 'java'
    apply plugin: spineCodestyleCheckerId

    // Verifies code style.
    // The parameters are same for any kind of verification:
    //
    //      - "threshold" - is a number of code style violations to consider check passed.
    //      - "reportType" - report type if a check is not passed. Can be either "error" or "warn".
    //              * "warn" - the violations are logged to the build report.
    //              * "error" - build will fail and its report will contain the violations.
    //
    //      For more details please see io.spine.tools.codestyle.CodestyleCheckerPlugin
    codestyleChecker {

        // Javadoc @link/@linkplain format checker.
        //
        // Finds and reports FQNs used in links in the wrong format.
        javadocLinkChecker {
            threshold = 0
            reportType = "warn"
        }

        // Right margin line checker.
        //
        // Finds and reports about the lines of a code with length above threshold.
        rightMarginWrappingChecker {
            maxTextWidth = 100
        }
    }

    // Specific setup for a Travis build,
    // which prevents appearing of warning messages in build logs.
    //
    // It is expected that warnings are viewed and analyzed in the local build logs.
    if (isTravis) {

        // Disable code style checks, which may produce many errors.
        tasks.findByPath('checkRightMarginWrapping').enabled = false
        tasks.findByPath('checkJavadocLink').enabled = false

        javadoc {

            // Set the maximum number of Javadoc warnings to print.
            //
            // If the parameter value is zero, all warnings will be printed.
            options.addStringOption('Xmaxwarns', '1')
        }
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // Set Java home to point to JDK7 in gradle.properties file.
    //
    // For Mac OS X, it looks like this:
    //
    // # suppress inspection "UnusedProperty"
    // org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk1.7.0_80.jdk/Contents/Home/

    repositories {
        jcenter()
        maven { url = googleMavenCentralMirror }
        mavenCentral()
        mavenLocal()
        maven { url = spineRepository }
        maven { url = spineSnapshotsRepository }
    }

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

        //As a Library, we provide logging facade API, not specific logger bindings.
        //Target apps are free to use any binding they need.
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4JVersion

        compile group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
        compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

        //Use jdk14 bindings for test purposes only.
        testCompile group: 'org.slf4j', name: 'slf4j-jdk14', version: slf4JVersion
        testCompile(group: 'junit', name: 'junit', version: jUnitVersion) {
            exclude(module: 'hamcrest-core')
        }
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: guavaVersion
    }

    sourceSets {
        main {
            proto.srcDirs = ["$sourcesRootDir/main/proto"]
            java.srcDirs = [generatedJavaDir, "$sourcesRootDir/main/java", generatedSpineDir]
            resources.srcDirs = ["$generatedRootDir/main/resources"]
        }
        test {
            proto.srcDirs = ["$sourcesRootDir/test/proto"]
            java.srcDirs = [generatedTestJavaDir, "$sourcesRootDir/test/java", generatedTestSpineDir]
            resources.srcDirs = ["$generatedRootDir/test/resources"]
        }
    }

    /* Uncomment this block if you need to display console output during the Gradle build.*/
    test {
        testLogging {
            // Make sure output from standard out or error is shown in Gradle output.
            showStandardStreams = false

            // Or we use events method:
            // events 'standard_out', 'standard_error'

            // Or set property events:
            // events = ['standard_out', 'standard_error']

            // Instead of string values we can
            // use enum values:
            // events org.gradle.api.tasks.testing.logging.TestLogEvent.STANDARD_OUT,
            //        org.gradle.api.tasks.testing.logging.TestLogEvent.STANDARD_ERROR,
        }
    }

    protobuf {
        // The below suppressions `GroovyAssignabilityCheck` is a workaround for the IDEA bug.
        // See: https://youtrack.jetbrains.com/issue/IDEA-141744
        //noinspection GroovyAssignabilityCheck
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$gRpcVersion"
            }
        }
        generatedFilesBaseDir = generatedRootDir
        protoc {
            artifact = protobufDependency
        }

        // The below code removes deprecation annotations from the code generated by
        // Protobuf compiler.
        // See details at the link below:
        //    https://github.com/google/protobuf/issues/2054
        generateProtoTasks {
            all().each { final task ->
                task.descriptorSetOptions.includeSourceInfo = true
                task.doLast {
                    ant.replaceregexp(
                            match:'@java.lang.Deprecated|@deprecated',
                            replace:'',
                            flags:'g',
                            byline:true
                    ) {
                        fileset(dir: "${protobuf.generatedFilesBaseDir}/main/java")

                        final generatedTests = "${protobuf.generatedFilesBaseDir}/test/java"
                        if (new File(generatedTests).exists()) {
                            fileset(dir: generatedTests)
                        }
                    }
                }
            }
        }
    }

    javadoc {
        source = sourceSets.main.allJava
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from ("$projectDir/build/docs/javadoc")
        classifier "javadoc"
    }

    // Apply the same IDEA module configuration for each of sub-projects.
    idea {
        module {
            generatedSourceDirs += file(generatedJavaDir)
            testSourceDirs += file(generatedTestJavaDir)
            downloadJavadoc = true
            downloadSources = true

            iml {
                beforeMerged { final module ->
                    module.dependencies.clear()
                }
                whenMerged { final module ->
                    module.dependencies*.exported = true
                }
            }
        }
    }

    // Apply the Javadoc publishing plugin.
    // This plugin *must* be applied here, not in the module `build.gradle` files.
    //
    if (shouldPublishJavadoc(project)) {
        apply from: updateDocsPlugin
    }
}

// IDEA project configuration.
idea {
    project {
        ipr {
            beforeMerged { final project ->
                project.modulePaths.clear()
            }
            withXml { final provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }
}
// Aggregated coverage report across all subprojects.
apply from: spineJacocoScript

apply from: publishPlugin

apply plugin: spineGcsPlugin

cleanGCS {
    final Properties properties = new Properties()
    final File gcsProperties = project.file("gcs.properties")
    properties.load(gcsProperties.newDataInputStream())

    authKeyPath = "gcs-auth-key.json"
    bucketName = properties.getProperty("artifacts.bucket")
    targetFolder = properties.getProperty("artifacts.folder")
    threshold = TimeCategory.getDays(10)
}
