//
// Copyright 2017, TeamDev Ltd. All rights reserved.
//
// Redistribution and use in source and/or binary forms, with or without
// modification, must retain the above copyright notice and the following
// disclaimer.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
//
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()

        // Spine releases repository.
        maven { url = 'http://maven.teamdev.com/repository/spine' }

        // Spine snapshots repository.
        maven { url = 'http://maven.teamdev.com/repository/spine-snapshots' }
    }

    ext {
        guavaVersion = '20.0'
        protobufGradlePluginVerison = '0.8.0'

        spineVersion = '0.8.1-SNAPSHOT'
        //TODO:2016-12-12:alexander.yevsyukov: Advance the plug-in version together with all the components and change
        // the version of the dependency below to `spineVersion` defined above.
        spineProtobufPluginVersion = '0.7.24-SNAPSHOT'
    }

    dependencies {
        classpath group: 'com.google.guava', name: 'guava', version: guavaVersion
        classpath(group: 'com.google.protobuf', name: 'protobuf-gradle-plugin',
                                                version: protobufGradlePluginVerison) {
            // exclude an old Guava version
            exclude group: 'com.google.guava'
        }
        classpath group: 'org.spine3.tools', name: 'protobuf-plugin',
                                             version: spineProtobufPluginVersion
                                            //, changing: true // Uncomment this if you want the dependency to be
                                            // fetched on each build, but offline builds will not be available then.
    }
    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'org.spine3'
    version = spineVersion
}

ext {
    // NOTE: when updating Protobuf dependency, please check that KnownTypes.addStandardProtobufTypes() method
    // is updated with new Message types introduced in the new version of Protobuf.
    protobufVersion = '3.2.0'
    protobufDependency = "com.google.protobuf:protoc:${protobufVersion}"
    gRpcVersion = '1.0.2'
    slf4JVersion = '1.7.21'
    jUnitVersion = '4.12'

    spineProtobufPluginId = 'org.spine3.tools.protobuf-plugin'
    publishPlugin = "$rootDir/scripts/publish.gradle"

    credentialsPropertyFile = 'credentials.properties'
    projectsToPublish = ["client", "server", "values", "testutil", "users"]
}

subprojects {

    project.ext {
        sourcesRootDir = "$projectDir/src"
        generatedRootDir = "$projectDir/generated"

        generatedJavaDir = "$generatedRootDir/main/java"
        generatedTestJavaDir = "$generatedRootDir/test/java"

        generatedGrpcDir = "$generatedRootDir/main/grpc"
        generatedTestGrpcDir = "$generatedRootDir/test/grpc"

        generatedSpineDir = "$generatedRootDir/main/spine"

        testArtifactsScript = "${rootDir}/scripts/test-artifacts.gradle"
        generateDescriptorSetScript = "${rootDir}/scripts/generate-descriptor-set.gradle"
        gRpcScript = "${rootDir}/scripts/grpc.gradle"
    }

    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // Set Java home to point to JDK7 in gradle.properties file.
    //
    // For Mac OS X, it looks like this:
    //
    // # suppress inspection "UnusedProperty"
    // org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk1.7.0_80.jdk/Contents/Home/

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

        //As a Library, we provide logging facade API, not specific logger bindings.
        //Target apps are free to use any binding they need.
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4JVersion

        compile group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
        compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

        //Use jdk14 bindings for test purposes only.
        testCompile group: 'org.slf4j', name: 'slf4j-jdk14', version: slf4JVersion
        testCompile(group: 'junit', name: 'junit', version: jUnitVersion) {
            exclude(module: 'hamcrest-core')
        }
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.+'
    }

    sourceSets {
        main {
            proto.srcDirs = ["$sourcesRootDir/main/proto"]
            java.srcDirs = [generatedJavaDir, "$sourcesRootDir/main/java", generatedSpineDir]
            resources.srcDirs = ["$generatedRootDir/main/resources"]
        }
        test {
            proto.srcDirs = ["$sourcesRootDir/test/proto"]
            java.srcDirs = [generatedTestJavaDir, "$sourcesRootDir/test/java"]
            resources.srcDirs = ["$generatedRootDir/test/resources"]
        }
    }

    /* Uncomment this block if you need to display console output during the Gradle build.
    test {
        testLogging {
            // Make sure output from standard out or error is shown in Gradle output.
            showStandardStreams = true

            // Or we use events method:
            // events 'standard_out', 'standard_error'

            // Or set property events:
            // events = ['standard_out', 'standard_error']

            // Instead of string values we can
            // use enum values:
            // events org.gradle.api.tasks.testing.logging.TestLogEvent.STANDARD_OUT,
            //        org.gradle.api.tasks.testing.logging.TestLogEvent.STANDARD_ERROR,
        }
    }
    */

    protobuf {
        // The below suppressions `GroovyAssignabilityCheck` is a workaround for the IDEA bug.
        // See: https://youtrack.jetbrains.com/issue/IDEA-141744
        //noinspection GroovyAssignabilityCheck
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$gRpcVersion"
            }
        }
        generatedFilesBaseDir = generatedRootDir
        protoc {
            artifact = protobufDependency
        }

        // The below code removes deprecation annotations from the code generated by Protobuf compiler.
        // See details at the link below:
        //    https://github.com/google/protobuf/issues/2054
        generateProtoTasks {
            all().each { final task ->
                task.doLast {
                    ant.replaceregexp(
                            match:'@java.lang.Deprecated|@deprecated',
                            replace:'',
                            flags:'g',
                            byline:true
                    ) {
                        fileset(dir: "${protobuf.generatedFilesBaseDir}/main/java")

                        final generatedTests = "${protobuf.generatedFilesBaseDir}/test/java"
                        if (new File(generatedTests).exists()) {
                            fileset(dir: generatedTests)
                        }
                    }
                }
            }
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    idea.module.iml {
        beforeMerged { final module ->
            module.dependencies.clear()
        }
        whenMerged { final module ->
            module.dependencies*.exported = true
        }
    }

    idea.module {
        generatedSourceDirs += file(generatedJavaDir)
        testSourceDirs += file(generatedTestJavaDir)
    }
}

idea.project.ipr {
    beforeMerged { final project ->
        project.modulePaths.clear()
    }
    withXml { final provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

// Aggregated coverage report across all subprojects.
apply from: "${rootDir}/scripts/jacoco.gradle"

apply from: publishPlugin
